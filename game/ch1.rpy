label ch1_s1:
    stop music fadeout 2.0
    "My father, Count Dequeris, had four daughters, much to his disappointment. No son to be his heir. The three oldest daughters came from his first wife. But as the youngest I, Celeste, had another mother."
    "I wasn't stupid but I was never the smartest. I was not the one best at math or knowing the history of the Empire. That title belonged to sister Aurora."
    "I wasn't ugly but I was never the prettiest. I was not the most graceful one or particularly eloquent in conversation. That was my sister Euridice."
    "I wasn't inept at magic but I was never the strongest.. I was not the one most powerful or capable of wielding such complex spells. My sister Lenaria was."
    "Many other titles were bestowed upon me instead. \"The Count's disgrace of a daughter\" was my father's favourite."
    "\"Good for nothing piece of trash\" is what my sisters called me. Sometimes people forgot my actual name."
    "Until my 18th birthday."
    jump ch1_s2

label ch1_s2:
    scene chapel with fade
    show celeste normal neutral at center with dissolve
    play music "celesteTheme.mp3" loop

    "I take one step forward and watch as the person in front of me goes towards the podium as the priest calls their name."
    "The Golden Bow, foretold to be the weapon to vanquish the Demon Queen and all her minions. "
    "The tool to defeat all evil stands upon its pedestal as colourful light from the stained glass windows falls upon it. Just looking at it, I can feel how sacred an artifact it is.."
    "The young man in front of me walks up to the podium. Based on his clothes, I deduce he is a commoner. But it's his 18th birthday as well, so he has the right to be in the palace too.."
    "It is customary that all the people of the Locari Empire upon reaching adulthood, come here to touch the bow to see if they are the chosen hero. The hero who will defeat the Demon Queen and bring peace to our land."
    "He touches the bow. I hold my breath, waiting. For what - I don't know. Am I hoping he will get chosen? No, I'm hoping he won't but why? Do I not want the war to end?"
    "The boy sighs and steps away from the podium. He is not the one. I feel... relieved? Do I crave to be someone special that badly? Me, who all her life was told I would amount to nothing?"
    "Better to get this over with. I am not hero material. I shouldn't fool myself, it won't be me."
    show celeste neutral at right with move
    show elijah neutral at left with dissolve
    "???" "Celeste Dequeris, fourth daughter of Count Dequeris."
    "As my name is called by the priest, I step up to the podium. As I do so I can't help but called by the priest and admire how the light glimmers on the bow's surface. I hesitate for a bit, but then place my hand on it."
    show celeste sad
    "I feel a presence that makes me want to cry. I never realised how alone I always felt but now I know, I won't be alone anymore."
    "Bow" "Not yet."
    "I hear a voice and then..."
    stop music
    show celeste crying
    # INSERT RINGING NOISE
    "Pain. Agony." with pain
    c "Ughhhh..."
    scene white with dissolve
    "Blinding light."

    # BLUR AND SHAKE EFFECT
    scene chapel
    show elijah surprised at left
    show levi concerned at center
    show celeste normal crying at right
    with dissolve
    l "Lady Celeste! What's going on?"
    "I hear footsteps... it's probably my knight, Levi coming to help."
    "???" "Could it be...?!"
    "I'm crying. I feel as if my body is falling apart, then rearranging itself."
    c "AAAAHHHHH!" with pain
    f "Celeste, what in the world possessed you to scream like that?!"
    "???" "My lord, it's her! Your daughter is the Saintess of the Golden Bow!"
    f "What?!"
    show levi surprised
    l "Lady Celeste!"
    "I hear my parents and sisters shout behind me. Other people too. Levi catches me as I fall."
    "Someone's screaming... is that me...?"
    "Finally the pain is too great. I pass out into blissful nothingness."
    scene black with dissolve
    l "Lady Celeste! Don't die!"
    jump ch1_s3

label ch1_s3:
    scene palace bedroom day with dissolve
    play music "celesteTheme.mp3" loop
    f "This cannot be true. I won't accept this! A good for nothing like her - a hero? Never!"
    "I hear the door slam as I slowly come to my senses."
    show celeste normal neutral at center with dissolve
    "I'm in a room... on a bed, still wearing my clothes. There's a damp cloth on my forehead but I don't feel feverish. No, I feel surprisingly good, more alive than ever."
    "Maid" "My lady, are you feeling unwell? Sir Elijah wanted to examine you."
    show elijah neutral at right
    show levi concerned at left
    with dissolve
    "I look at the Priest of the Goddess of Light standing next to Levi. He's half a head shorter than my knight but still taller than me. His hair is a bright magenta, quite an unusual color. He has a wide, warm smile on his face."
    "I believe I have heard of Sir Elijah before. Was that not the priest my sister Euridice took a liking to? I'm almost certain he rejected her, she threw quite a tantrum. I didn't know much about him beyond that."
    show elijah happy
    e "Don't be afraid, Saintess. I just do dome tests, a routine check-up - magical of course."
    c "Alright."
    "Sir Elijah leans over me and passes his hands over me without touching. His palms are glowing with warm golden light. I feel a sense of peace whenever his mana touches me. It's pleasant, like a brush of sun rays at the beginning of spring. I sigh, content."
    e "I see you are enjoying the Goddess' touch."
    "He whispers jokingly, only for me to hear and winks at me. I can't help but feel embarrassed when he points it out. It's supposed to be a simple check-up, why am I enjoying this so much?"
    "I've always been sensitive to holy powers but somehow now that I've been chosen by the bow... I feel them more intensely?"
    show levi neutral
    e "All is well with you, Saintess. You're in top form in fact. No injuries or illnesses."
    "Sir Elijah moves away, taking his pleasant light with him. I miss the warmth of the Goddess. It almost felt like being hugged by her grace. Oh well, I've lived without hugs all my life. No big deal."
    "Maid" "If you are feeling well, my Lady, the Empress and Emperor consort would like to speak with you."
    c "Empress Mirelle and Emperor Lucius would like to speak with me?"
    "I repeat the words, I cannot believe them. I've only seen the Empress and her husband from afar during the few balls and parades my father would allow me to attend.."
    "They want to speak with me now?"
    c "But.. why... me...?"
    "Maid" "It's about the Golden Bow..."
    "It's clear the maid is uncomfortable."
    show levi happy
    l "Yes, indeed the Bow chose you as the Saintess, my Lady. Congratulations!"
    "Come to think of it, the priest was calling me the Saintess. I guess I was too distracted by his magical powers to notice."
    "Now it all comes back to me - touching the bow, the pain... I'm 18 today and I became the Saintess of the Golden Bow. The hero who will defeat the evil Demon Queen and end the war..."
    "Me, the good for nothing Celeste Dequeris."
    c "Me... the Saintess..."
    e "Congratulations. You have been chosen as our hero."
    l "Yes, the hero that was foretold. That's you, my Lady!"
    "I nod my head and get out of bed."
    "With my head bowed low, hair covering my face, I follow the maid to the throne room. Levi and Sir Elijah come too."
    jump ch1_s4

label ch1_s4:
    scene palace throne day
    show celeste normal sad at center
    show levi neutral at right
    show elijah neutral at left
    with fade

    f "She is incapable of being the Saintess! All her life she has truly amounted to nothing! My Imperial Highnesses, you have to believe me. This is some kind of farce!"
    "Empress Mirelle" "Do you question the divine judgement of the Golden Bow, Count Dequeris?"
    f "No, I..."
    "I stand quietly in the doorway to the throne room, trying to be invisible. It's nothing new to me, I've been doing this all my life."
    "Sir Elijah looks at me with concern. Levi stands next to me, anger brewing within him. He controls it but I can still see the fury on his face.  Scary..."
    "Empress Mirelle is as radiant as ever, with her long blonde hair in an elaborate chignon. Her husband, Emperor consort Lucius, is sitting on the throne next to her. I wonder if he misses the warmth of the southern lands."
    "Emperor Lucius" "The bow chose the young lady for a reason..."
    f "But it isn't possible! There has to be some kind of mistake."
    "Empress Mirelle" "No, there could not have been. You saw her body glow as the divine power was absorbed."
    "Emperor Lucius" "It's done. All we can do now is trust the bow's judgement."
    show celeste neutral
    "It feels good that someone's defending me. That happens so rarely, almost never."
    show celeste determined
    "Built up by the Imperial Highnesses believing in me, I step towards the throne and stand next to my father."
    hide levi
    hide elijah
    with dissolve
    "Levi stands firm, guarding the door and Sir Elijah joins him."
    "The queen and king smile at me, while father scowls. But that's nothing new. His reaction does not surprise me."
    "I present my best curtsy. I'm not as graceful as Sister Euridice, but I won't fail to show respect to those who believe in me."
    "It might not be the best curtsy ever but the Empress and Emperor are still smiling when I raise my head. I sigh inward, careful not to let the nerves betray my expression."
    c "My Empress. Emperor consort. Father."
    f "Allow me to introduce my daughter, Celeste Dequeris."
    "He almost chokes at the word daughter. Very well, I will play my part."
    c "I am Celeste, fourth daughter of Count Dequeris."
    "Empress Mirelle" "I'm so pleased to meet you, Lady Celeste!"
    "The Empress smiles at me brightly."
    "Emperor Lucius" "As am I."
    "Emperor Lucius is more reserved. I see him look my small form up and down. He's assessing my strength... or the lack thereof."
    "How do I pretend to be someone I am not? A warrior, a hero?"
    "Emperor Lucius" "So are you good with a bow, Celeste?"
    "Archery is actually the only thing I'm good at, but I won't say that."
    show celeste neutral
    c "I think so, my Emperor."
    "Emperor Lucius" "You think so... Are you unsure?"
    "That remark makes me even more nervous. I fidget slightly."
    show celeste sad
    show noah prince angry at offscreenleft
    c "I mean..."
    "Father snorts. I'm back at home where my every word is questioned."
    "I want to cry. I hate myself for it. I'm so weak, but crying is not the answer to anything."
    "I'm supposed to show how strong I am, yet I want to cry in front of the very people I'm supposed to convince."
    stop music fadeout 1.0
    "How can I convince someone else, if I do not believe it myself..."
    show celeste neutral at right
    show noah prince angry at left
    with move
    play music "noahTheme.mp3" loop
    "The door to the throne room opens with a loud bang. With a flutter of his cape and marching long strides, Prince Noah, the heir to the throne enters. His expression is as fierce and scary as always." with sshake
    "I'm saved, but I don't feel safe at all."
    n "You called, Mother, Father?"
    "Only when he comes nearer the throne, does he notice us."
    show noah neutral
    n "Count Dequeris. And who is that?"
    "He nods at my father and looks me up and down. There's a strange glint in his eye that I do not recognise. When our eyes meet, he quickly looks away."
    "The heir is often called the Sun of the Empire."
    "He is truly a handsome man, with blond hair and tan skin, a mixture of his northern and southern heritage. But his permanent expression of anger and piercing red gaze makes him seem rather intimidating."
    "I realise that I'm staring and I look away."
    "The Empress is glancing at me with a knowing expression."
    "I blush. So what if I find him pretty? My thoughts and feelings are insignificant."
    "Emperor Lucius" "Celeste Dequeris, now the Saintess of the Golden Bow."
    show noah surprised
    n "The Saintess of the Golden Bow?! That weakling?"
    "It does not hurt, it does not hurt. I have no pride, there's nothing to hurt."
    show noah neutral
    n "Wait... Dequeris, Dequeris... I know who you are... you're the youngest of Count Dequeris' daughters... the one that's called a failure!"
    show celeste sad
    "He smiles at the end and I freeze. Somehow it hurts, when someone other than my family says that to my face. But why? It's what I am."
    "Why did I think anything would change if I became the Saintess? The only thing that has changed is the weight of expectations on my back... no one expected anything from me and now they expect so much. Now, they really do have every right to judge me."
    "After all, the royal family and their citizens have waited years for the Saintess... and they got me."
    "I stand there frozen, unhearing.The conversation continues around me, about me? No, about the person who is the Saintess... but that's me..."
    "Emperor Lucius" "Noah!"
    n "What? I just said what you all are thinking anyway. But you won't say it because of etiquette. It's the truth and it should be said."
    "Empress Mirelle" "I believe in the Bow's ability to choose our saviour!"
    show noah angry
    n "The bow?! It's just a piece of trash if it chose some weakling. Can you even shoot a bow, weakling?"
    "He looks at me expectaly. I want to cry again. I'm not saved, it's worse. I..."
    c "... yes."
    n "What did you say? I can't hear you!"
    "I try again, but my voice fails me."
    c "...yes..."
    n "This is pointless. Even if you can shoot, you're not even capable of killing a fly. You're not a warrior, you're a weakling.You don't have it in you."
    c "..."
    "I don't reply. It's the truth, so what can I say?"
    "Empress Mirelle" "Enough!"
    "Empress Mirelle" "She said she can wield a bow, can you vouch for that, my dear Count Dequeris?"
    "Father hesitates. But I'm so hurt, I don't feel anything anymore. He knows I'm good with the bow, but still, he hesitates."
    f "Yes, absolutely, my Queen, she's very good at it."
    "I don't think anyone believed him."
    "Empress Mirelle" "Very well then! Tonight we will host a feast to announce that the Saintess has appeared..."
    show noah angry
    n "...pha."
    "Empress Mirelle" "And tomorrow morning, Noah, you will take twenty of our best knights and mages and leave with the Saintess to defeat the Demon Queen!"
    show noah surprised
    "Silence. No one says a thing. Noah has a face of absolute shock... it looks sort of funny... I must be going crazy if I want to laugh in this situation. We are being sent to our doom, and I think this is funny...?"
    n "..you've got to be kidding me..."
    "Empress Mirelle" "Pardon me, son?"
    "Emperor Lucius" "Noah...!"
    "There is the sound of warning in Emperor Lucius' voice..."
    show noah angry
    n "I don't think this..."
    "Empress Mirelle" "We've waited for so long for a means to end the war... We will celebrate and then you will lead the mission. I expect nothing but good results. After all the gods have finally blessed us with the Saintess!"
    n "..."
    show noah surprised
    n "I will respect your wishes, oh Mother, Father!"
    "His voice is mocking, but he backs down. Then, he turns around and leaves the room."
    stop music fadeout 1.0
    "Empress Mirelle" "You may use the room you woke up in during your stay with us, Saintess. By the gods be blessed and see you this evening!"
    "With that, father bows, I curtsy and we take our leave."
    jump ch1_s5

label ch1_s5:
    scene palace corridor
    show celeste normal sad at center
    with fade
    play music "celesteTheme.mp3" fadein 1.0

    "As we exit the throne room, I can't help but notice how angry Father is. I know exactly what's coming... I just hope the beating won't leave such bruises as last time."
    "Yet, like the dutiful daughter I am, I walk with him towards the guest room."
    show celeste sad at left with move
    show levi neutral at right with dissolve
    "Levi follows behind us, a stern expression across his face. I wonder if he knows."
    "This is embarrassing. I don't want anyone to see me as weak, to take pity on me. Especially not Levi..."
    "As we walk to the guest room, I notice a group of people. They're in bad shape, I can see parts of their skin burned and raw."
    "Refugee" "My Lady! My Lord! Help us!"
    "My father scoffs and ignores them. But I can't. I stop and so does Levi. I'll get a beating whatever I do, I have nothing to lose."
    show celeste surprised
    c "What's wrong? How can I help?"
    "Refugee" "My Lady! It's the demons! They attacked our village!"
    "Refugee" "The Demon Queen's army is advancing, they destroyed Barwood!"
    c "You should ask the Empress and Emperor for a hearing!"
    "Refugee" "We have... we've been waiting here for hours..."
    "Refugee" "Momma, I'm hungry!"
    show celeste sad
    "I feel guilty. My hearing must have delayed theirs, but they've been here for hours... the Empress must really not want to see them, but why?"
    "Refugee" "Hush, love. I don't have anything to give you."
    "I look at my Father. He is waiting at the end of the corridor, ambivalently looking away."
    "I put a hand to my ear and clasp the ruby earring. I take both off and give them to the woman."
    show celeste determined
    c "Here. I hope this will help you start your life anew in the capital."
    "I don't know the worth of my gift, but the people start to cry. I hope it will be enough."
    show levi happy
    "I look at Levi, who smiles at me. There's admiration and gratefulness in his eyes. I know he comes from a village that was destroyed by the Demon Queen's army too."
    c "Levi."
    l "Yes, my lady!"
    c "Take some of the refugees and go to the kitchen. Say I've sent you, they know us. They should be able to give those people something to eat."
    l "Yes, my lady! On it!"
    show levi neutral
    "He chooses a few of the refugees that look like they are in better shape and leaves for the kitchens."
    show levi neutral at offscreenright with move
    show celeste neutral
    "I walk towards my father. Away from the pain and doom of others and towards my own."
    show celeste sad at flipLeft
    "No one will save me from my pain. But if I can lessen the pain of others, I will."
    jump ch1_s6

label ch1_s6:
    scene palace bedroom day
    show celeste normal sad at center
    with fade

    "As we enter the guest room, I see the rest of my family gathered around the coffee table. I sigh with relief. I am no longer subject to the scrutiny of strangers -"
    "Smack. I fall to the floor. I feel the red hot mark of father's hand across my face." with pain
    "Now, it is not the stranger's scrutiny I must endure, but my own family's.."
    "My sisters laugh and mother just looks away. She always does. She never participates in the torment, nor does she try to stop it. That makes me hate her as much as I do the rest of them."
    "Sister Euridice" "Saintess of the Golden Bow! Haha, what a joke!"
    "Sister Aurora" "It can't really be Celeste, there must be some kind of mistake!"
    "Sister Lenaria" "Celeste is a piece of lowly filth, a beautiful artifact like the Golden Bow would not have chosen her!"
    "I pick myself up from the ground, as father walks towards my sisters and mother."
    f "I believe it is unfortunately true."
    "My sisters all gasp, shocked. And once again, I think this is rather funny... I really must be out of my mind."
    f "What are you smiling at... you good for nothing daughter. You ought to think about how you intend to not to embarrass us on the journey with the Prince!"
    f "I don't care if you die in the process, but you must defeat the Demon Queen!"
    f "Or else, such shame will fall upon the Dequeris family. Such shame... You couldn't even imagine!"
    c "Yes, Father..."
    show celeste surprised
    c "I will defeat the Demon Queen!"
    "I put as much conviction behind my words as I can. I don't know whom I am trying to convince more - my family or myself."
    jump ch1_s7

label ch1_s7:
    scene palace bedroom day
    show celeste normal sad at center
    show levi concerned at offscreenleft
    with fade
    stop music fadeout 1.0
    "My family finally leave me and I feel the weight of expectations growing heavier.."
    play music "leviTheme.mp3" fadein 1.0 loop
    "A while later, there's a knock on the door."
    l "My Lady... can I speak with you?"
    c "Yes, please come in, Levi."
    show levi surprised at left
    show celeste sad at right
    with move
    "Levi enters the room, he immediately notices the redness of my cheek and fury overcomes him. He always gets angry on my behalf."
    show levi concerned
    l "My Lady!"
    show celeste neutral
    "It's nice to have someone care about you. But I can't help but feel concerned for him. What if my family decides to get rid of him? What if they decide I do not deserve to be protected?"
    show celeste sad
    "Then I will be even more alone."
    "What a selfish thought."
    show celeste neutral
    c "Don't worry about me, Levi. I'm fine."
    show levi angry
    l "Your Father, he- he slapped you! Again! You don't have to feel fine."
    c "But I really am fine, Levi. It's nothing."
    "I'm not fine, but I don't want him to worry about me too much."
    show levi concerned
    l "Alright, my Lady. If you say so... So, you are the Saintess...how do you feel about it all?"
    c "I... don't know. I... never thought it would be me."
    show levi happy
    l "Why not? You are kind and brilliant at archery!"
    show celeste sad
    c "But I'm not hero material. Everyone knows that..."
    show levi neutral
    l "... but you are! I truly believe that!"
    show celeste neutral
    c "Thank you for saying that, Levi! Even though it's not true."
    show levi sad
    l "...Hmm."
    "He looks like he wants to say something, but decides not to. Sometimes I feel like he would overstep the boundary of knight and Lady if I allowed it."
    show celeste sad
    "I cannot allow it. He's my only friend and I don't want to be... alone."
    "I ask him to leave the room as I have to prepare for the feast. With a sad and thoughtful expression, he leaves."
    show levi sad at offscreenleft with move
    "He goes to his post outside the door, taking watch as my faithful knight."
    stop music fadeout 1.0
    "We all have to play our parts."
    jump ch1_s8

label ch1_s8:
    scene palace throne night
    show celeste fancy neutral at right
    show noah prince angry at left
    with fade
    play music "celesteTheme.mp3" fadein 1.0 loop

    "Empress Mirelle" "I welcome you all on this joyous day. A day I thank the gods for gifting us!"
    "Empress Mirelle looks positively radiant in her finest robes, as she addresses the guests. Next to her, her husband stands tall with pride, wearing the ceremonial clothes of the south."
    "In front of this father, below the podium on which the thrones sit, stands Prince Noah."
    "He did not bother changing clothes for the occasion. But he always dresses quite stylishly, so he does not look out of place."
    "He's so handsome it hurts, but then there's that scowl across his brow.... Scary..."
    "Empress Mirelle" "Lady Celeste Dequeris, please come forward!"
    show celeste sad
    "On shaking legs, I make my way through the crowd as it parts in front of me. I arrive in front of the throne, next to Prince Noah."
    show noah neutral
    n "Turn around and face the crowd."
    "I turn around. My stomach flips, as I see all those faces staring at me. Before I can collect myself, I hear the murmurs of the crowd. They don't even try to hush their words."
    "Noble Lady 1" "Isn't she {i}that{/i} Dequeris daughter?"
    "Noble Lady 2" "Yes, it must be!"
    "Noble Lord 1" "What do the Empress and Emperor consort want with someone like her?"
    "Emperor consort Lucius clears his throat and the room quietens."
    "Empress Mirelle" "It was foretold that only the Saint of the Golden Bow can wield the weapon. The very weapon that will defeat the scourge of the wretched Demon Queen."
    "Empress Mirelle" "Thus a tradition was born. When each citizen reached adulthood, they came here to lay a hand upon the bow.  To see if they would be chosen by it, if they were destined to be the Saint."
    "Empress Mirelle" "Today, I am pleased to announce that the search is over. Before you stands the Saintess of the Golden Bow, chosen as foretold. Please welcome Lady Celeste Dequeris as our saviour!"
    "The room goes quiet. I can almost hear myself breathing."
    show noah angry
    n "What are you waiting for?! Kneel before the Saintess!"
    "One by one, they all kneel, but I can see how reluctant they are."
    "This is beyond awkward. I never thought I would have anyone kneel before me. Even a bow is too much respect for someone like me... but... I'm the Saintess now."
    n "Stand up! Cheer!"
    "The crowd cheers weakly. I can feel the weight of disappointment. They're clearly far more enthusiastic to stand again, rather than to cheer and congratulate me.."
    "Empress Mirelle" "The Saintess and Prince Noah, the Sun of the Empire, will depart tomorrow morning with a group of our best knights and mages."
    "Empress Mirelle" "They will make the arduous journey to the Demon Queen's Castle and slay the vile foe that has wrought havoc across our lands for so many years."
    "Empress Mirelle" "Now, let us feast and celebrate this most joyous occasion!"
    "She claps her hands and the servants open the doors to the next room where the feast awaits. The table is piled high with so many different dishes, it almost looks like it could break under the weight."
    "I have not eaten since morning, but the thought of eating anything under these circumstances makes me feel sick."
    "Still I sit down as all the other guests stare at me in disapproval. I can feel their constant judgement as I begin to nibble at some food."
    "No one tries to engage me in conversation but I hear them talking about me. They don't trust me to save them, they feel doomed. This is more like a funeral than a celebratory feast."
    hide noah with dissolve
    show celeste fancy sad at center with move
    "Much to his parents' disapproval, Prince Noah storms out of the room as soon as he finishes eating. He has to prepare for the journey, I think. I suppose that's why no one stops him."
    "I sit there staring at the half eaten food on my plate. I listen to the guests belittle and ridicule  me. Until finally, I can take no more.."
    "I don't care about decorum. I don't care about their expectations. I just need to get out of here."
    "I leave the dining room and head to the throne room. There, I see the door to the terrace. I wait a moment until there are only the servants to see me and I dart out, running down the stairs to the garden."
    jump ch1_s9

label ch1_s9:
    scene palace garden night
    show celeste fancy sad at center
    with fade

    "The royal garden is a maze of beautiful flowers, masterfully cut hedges and exquisite topiaries. I barely notice any of it as I run with tears in my eyes."
    "I just want to get away from it all as far away as possible."
    "Finally, I reach a pond lit only by a singular magical lantern. I can no longer hear the sounds of the celebration and the lights of the palace are far behind me."
    "For the first time today, I am alone."
    show celeste crying
    "I fall on my knees, not caring if my dress gets dirty."
    "My image is already ruined beyond repair by the vicious rumors spread by my sisters. Throughout the years they have worked and worked to ensure they will always be superior when compared to the ugly duckling... when compared to me. ."
    "And they succeeded. Now no one believes I can save them. No one thinks I can amount to anything."
    "Only the gods do, they have chosen me. But what if this was some kind of mistake?"
    "Can the gods be mistaken? Why me? Of all people, why me?!"
    "I realise I'm crying. And more tears come... I cry harder and harder."
    show noah prince angry at offscreenleft
    n "Pathetic..."
    show celeste crying at right
    show noah angry at left
    with move
    stop music fadeout 1.0
    play music "noahTheme.mp3" fadein 1.0 loop
    "I raise my head and through tears I see Prince Noah approaching. He takes out his sword and points it right at my throat."
    # NOAH CG: I SHOULD KILL YOU
    # REPLACE THESE SPRITES WITH THE CH WHEN YOU GET IT
    show celeste surprised
    c "Huh!"
    n "I should just end you here and be done with it. We'll all die in the Demon Queen's castle anyway."
    c "Then do it."
    show celeste sad
    "I feel the coldness of steel pressing against my throat. I feel calm. If this is the end, if this is the angel of death standing in front of me, then so be it."
    n "I..."
    "I see some kind of emotion break through on his face, but all too quickly his features revert to his usual scowl."
    show noah surprised
    n "Do you truly believe me to be so cold-hearted as to kill a defenceless woman in cold blood?"
    c "...eh."
    n "Forget it..."
    "He pulls his sword away and seaths it."
    show noah sad
    n "We will die when the gods will it. No sooner, no later."
    "He's acting so strangely, why? I can't quite place my finger on it.? One moment he calls me good for nothing, the next he wants to kill me but now..."
    "...he just looks so sad.."
    show noah surprised
    "He turns to leave but I-"
    show celeste surprised
    c "Wait!"
    "I call out to him, he turns around. But the words escape me."
    "I don't know what to say."
    show celeste sad
    c "No, it's nothing..."
    n "See you tomorrow then. Don't oversleep, if you can get any sleep tonight at all."
    show noah surprised at offscreenleft with move
    stop music fadeout 1.0
    "I nod and watch him leave."
    jump ch1_s10

label ch1_s10:
    scene chapel NIGHT
    show celeste fancy sad at right
    with fade
    play music "celesteTheme.mp3" fadein 1.0 loop
    "When I feel sad and hopeless like I do right now, my feet usually carry me to the chapel of the Goddess of Light. We have one in our mansion too but it's very small. The palace's chapel is grand."
    "The Golden Bow is still standing on its pedestal. I suppose it will be removed tomorrow when we leave for the Demon Queen's castle."
    "It's a beautiful weapon. One could even call it a work of art. The gentle curve and bend of the bow, the way the edges flair and curl, as though it were a pair of wings ready to take flight."
    "It was crafted a few decades ago by a blacksmith blessed by the Goddess. He said he fell into a trance while She guided his hand."
    "When it was completed the blacksmith received the prophecy that one day the Golden Bow will choose the Saintess, the only person capable of defeating the Demon Queen. Until then, the world had to prevail."
    "Decades passed as the demons ravaged more and more of our land and moved closer and closer to the heart of the Empire."
    "I put my hands together in prayer. I recite the usual words of worship under my breath but they feel empty today. I'm no longer just a simple woman, I am the Goddess' weapon. Her bow."
    show elijah happy at offscreenright
    c "How do I pray for guidance?"
    e "Just put your hands together and say \"dear Goddess, defeat all my enemies\" and boom done!"
    show celeste sad at left
    show elijah happy at right
    with move
    "I'm startled when I hear the priest's voice behind me. I turn around and see Sir Elijah approaching, a bright smile on his face."
    c "I wish it was so simple."
    show elijah neutral
    e "It will be. All you have to do is believe you are the right person for the job. The Goddess made Her choice."
    c "But... I've always been told I'm not good enough. Now suddenly I'm someone special and I'm supposed to change the way I see myself in one day?"
    show celeste crying
    "I want to cry again when I think about it. Yes, I always wanted to be seen differently. However, it comes with a weight of responsibility I'm not sure I'm ready for."
    show elijah surprised
    e "Hey... don't cry, Saintess. All will be well. We walk in the Goddess' grace."
    show celeste sad
    c "I believe in the Goddess of Light. But I can't say my faith is as strong as yours. If this is a test from Her, I will need all my strength to pass it."
    show elijah neutral
    e "I'm sure-"
    "Sister Euridice" "Sir Elijah! Where are you?"
    "Our conversation is interrupted by my sister Euridice calling Sir Elijah from the corridor outside of the chapel."
    show elijah surprised
    e "Crap! We gotta hide!"
    "Sir Elijah grabs me by the hand and pulls me in the direction of a door in the chapel."
menu:
    "Go with him":
        $ hideWithElijah = True
        jump ch1_s10_1
    "Stay put":
        $ hideWithElijah = False
        jump ch1_s10_2

label ch1_s10_1:
    scene black with dissolve
    "Before I can reply, I'm dragged by the priest through a door on the opposite side of the corridor. The darkness envelops us and with the smell of cleaning supplies I realise we are... in a closet."
    show celeste fancy neutral at right
    show elijah neutral at left
    c "..."
    # CG: HIDING WITH ELIJAH
    "There is very little room here. The priests' body presses closely against mine as he leans over me to peek through the gap in the door.."
    "Sister Euridice" "I know you're here! You can't hide from me!"
    show elijah angry
    e "Tsk, woman, can't you just leave me alone!"
    "With my nose in Sir Elijah's chest I can't help but notice how nice he smells - warm and aromatic like the herbs and healing potions made by the priests."
    "I can feel the magic pulsing through his body in a steady rhythm with his heartbeat."
    "Sir Elijah's magic feels comforting and peaceful, like the grace of Goddess itself. Lured by the pleasant feeling I lean in and put my head on his chest."
    show elijah surprsied
    e "Saintess...!"
    "I realise what I'm doing and want to pull away but Sir Elijah puts his arms around me instead and hugs me to his chest."
    show elijah shy
    e "N-no, you can stay like this."
    "Oh. It feels so nice to be hugged. I tell myself not to think and just enjoy the moment. If I stopped to wonder, I would question why the priest pulled me in here with him in the first place."
    "Sister Euridice" "I swear Sir Elijah was here- Ohhh... where is he?"
    "We hear my sister's footsteps leaving the chapel."
    "Sir Elijah waits a minute longer just to be sure, still holding me in a hug. Eventually, he lets go  - all good things have to come to an end I suppose. He lets me go and we exit the closet."
    jump ch1_s10_3

label ch1_s10_2:
    show celeste surprised
    "I won't let a stranger drag me around. Why would I need to hide with him? My sister is his problem!"
    "Sir Elijah looks at me exasperated, then let's me go and runs to the door on his own. He's safely hidden when Sister Euridice enters the chapel."
    hide elijah surprised with dissolve
    show celeste neutral at center with move
    "Sister Euridice" "I know you're here! You can't hide from me!"
    show celeste sad
    "Sister Euridice" "Oh. The good-for-nothing Celeste is here. Came to pray because that's the only thing that you can do?"
    "Sister Euridice" "Are you just going to sit here and pray for a miracle? I mean, how else can you hope to be called a Saintess!"
    "I let my sister rant all she wants. She's right after all. I'm not good enough to be the hero of the realm."
    "Sister Euridice" "Cat got your tongue? Well, that's your only virtue - you don't talk back! Now, where is Sir Elijah?"
    c "Not here. I'm many things, but blind I am not. I would have noticed him if he were."
    "Sister Euridice" "Hm, alright. I get your point. I gotta look elsewhere then."
    "Sister Euridice leaves. She didn't expect me to lie. Phew. I wait a minute longer to be sure."
    show celeste neutral
    show elijah neutral at offscreenleft
    c "It's safe to come out now."
    show elijah neutral at left
    show celeste neutral at right
    with move
    "The priest exits what I now realise is the cleaning supplies cabinet."
    c "I thought you rejected Sister Euridice. Why is she still bothering you?"
    show elijah angry
    e "Yes, I thought I was clear about not being interested in her. I wish I knew how that didn't through to her."

label ch1_s10_3:
    if not hideWithElijah:
        show elijah angry
        e "She's even more vicious than I thought she was. They way she treats you. Disgusting."
        c "I'm used to it."
        "Sir Elijah looks at me with pity and I can't help but feel put off. I don't need anyone's pity."
    else:
        scene chapel night
        show celeste fancy neutral at right
        show elijah neutral at left
        with fade

    c "I wish I could talk to her and help you. But she never listens to me."
    e "No matter. We are leaving for the Demon Queen's castle tomorrow. I won't be seeing her for a while... Maybe she'll calm down in the meantime."
    c "You're coming with us?"
    show elijah happy
    e "Of course. I'm a combat priest, I'm trained in healing all manner of battle wounds. My friend and I, Mark, will be your healers on this journey."
    "I'm happy he's coming with us. The priests are often very solemn and serious yet Sir Elijah is quite the opposite. He'll be a fun companion to have around-"
    "Should I want to have fun on such an important journey though? Oh well. No one needs to know what I'm thinking."
    show celeste happy
    c "I'll be happy to have you as part of our team."
    e "Hey, you smiled! Thank the Goddess!"
    show celeste neutral
    "I look at Sir Elijah surprised."
    show elijah sad
    e "And... it's gone..."
    "I sigh. It might be fun, but maybe it won't be so easy to have him around after all."

    scene black with dissolve
    stop music fadeout 1.0
    "I head back to my guest room."
    jump ch1_s11

label ch1_s11:
    scene palace bedroom night
    show celeste fancy sad at right
    show levi sad at left
    with fade
    play music "leviTheme.mp3" fadein 1.0 loop

    l "My Lady... were you crying...?"
menu:
    "Deny it":
        c "No, I was not..."
        "But it must be visible on my face."
        l "Why are you lying to me, my Lady?"
        "He looks hurt. He saw right through me."
        jump ch1_s11_1
    "Admit it":
        "I want to deny it, but I know it must be easy to see..."
        c "I'm sorry... I'm so weak..."
        show levi angry
        l "You are not, my Lady! You are the strongest person I know! Despite what all of those people say and do to you, you still survive."
        jump ch1_s11_1

label ch1_s11_1:
    show levi sad
    l "If I could I..."
    show celeste surprised
    show levi surprised
    c "Whatever you want to say, don't say it."
    show celeste sad
    show levi sad
    c "We are knight and Lady. We cannot truly be friends. Please don't overstep the boundary."
    l "..."
    l "I understand, my Lady."
    show levi neutral
    l "I will support you on your quest, and I will follow you until the end as your loyal knight."
    show celeste neutral
    c "Thank you, Levi."
    stop music fadeout 1.0
    jump ch2_s1

label ch2_s1:


    "*rest of the script*"
return
